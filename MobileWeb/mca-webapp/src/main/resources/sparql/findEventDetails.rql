PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ical:      <http://www.w3.org/2002/12/cal/ical#>
PREFIX mca:     <http://vocab.bris.ac.uk/mca/registry#>

CONSTRUCT {
    $this mca:hasEventItem $uid .
    $uid ical:dtstart $startdate .
    $uid ical:dtend $enddate .
    $uid ical:description $description .
    $uid ical:summary $summary .
    $uid ical:location $location .
    $uid ical:organizer $orgname .
    $uid ical:calAddress $orgemail .
    $uid ical:uid $id .
    $uid mca:template <template://eventDetails.ftl> .
    $uid rdf:type ical:Vevent .
    $uid mca:hasParent $this .
    $uid rdfs:label $summary .
 } WHERE {
    GRAPH ?graph {
        $x ical:uid $id .
        $x ical:dtstart $y .
        $y ical:dateTime $startdate .
        OPTIONAL { $x ical:description $description . }
        $x ical:summary $summary .
        $x rdf:type ical:Vevent .
        OPTIONAL
         {
            $x ical:dtend $z .
            $z ical:dateTime $enddate .
         }
        OPTIONAL { $x ical:location $location . }
        OPTIONAL { $x ical:organizer [ ical:cn $orgname ] . }
        OPTIONAL { $x ical:calAddress $orgemail . }
    }
}
